plugins {
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'org.beryx.jlink' version '2.22.3'
}

javafx {
    version = "11.0.2"
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.graphics', 'javafx.base']
}

mainClassName = "app.main/cc.cc1234.PrettyZooApplication"
jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    addExtraDependencies("javafx")
    forceMerge("jackson", "log4j-api")
    launcher {
        name = 'prettyZoo'
    }
    jpackage {
        def os = org.gradle.internal.os.OperatingSystem.current()
        if(os.windows) {
            installerType = 'exe'
            installerOptions = [
                    '--name', 'PrettyZoo',
                    '--app-version', "${project.version}",
                    '--description', 'Pretty nice Zookeeper GUI',
                    '--copyright', 'Copyrigth',
                    '--vendor', 'vran',
                    '--win-per-user-install', '--win-dir-chooser',
                    '--win-shortcut'
            ]
        }
    }
}

jpackage {
    doLast {
        file("${buildDir}/jpackage/prettyZoo-${version}.dmg").renameTo("${buildDir}/jpackage/prettyZoo-mac.dmg")
        file("${buildDir}/jpackage/prettyZoo-${version}.exe").renameTo("${buildDir}/jpackage/prettyZoo-win.exe")
    }
}

dependencies {
    compile project(':domain')
    compile project(':spi')
    testCompile group: 'junit', name: 'junit', version: '4.12'
//    compile group: 'org.fxmisc.richtext', name: 'richtextfx', version: '0.10.2'
}

configJavafxRun {
    dependsOn classes
}